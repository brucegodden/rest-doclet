allprojects {
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'artifactory'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  group = 'org.cloudifysource'
  version = '0.5.31'

  task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task createPom << {
    pom {}.writeTo("$buildDir/libs/pom.xml")
  }

  artifacts {
    archives sourceJar
  }

  assemble {
    dependsOn sourceJar
    dependsOn createPom
  }

  artifactory {
    publish {
      contextUrl = "$artifactory_contextUrl"
      repository {
        repoKey = 'libs-release-local'
        username = "$artifactory_user"
        password = "$artifactory_password"
        ivy {
          ivyLayout = '[organization]/[module]/[revision]/ivy-[revision](-[classifier]).xml'
          artifactLayout = '[organization]/[module]/[revision]/[module]-[revision](-[classifier]).[ext]'
          mavenCompatible = true
        }
      }
      defaults {
        publishIvy = true
      }
    }
  }
}

repositories {
  mavenCentral()
}

buildscript {
  repositories {
    maven {
      url "${artifactory_contextUrl}/libs-release"
      credentials {
        username "$artifactory_user"
        password "$artifactory_password"
      }
    }
  }

  dependencies {
    classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.2.4'
  }
}

dependencies {
  ext {
    jackson_version = '1.9.13'
    spring_version = '4.0.1.RELEASE'
  }

  compile 'cglib:cglib-nodep:2.2'
  compile 'com.fasterxml.jackson.core:jackson-annotations:2.4.3'
  compile 'com.google.code.findbugs:jsr305:3.0.0'
  compile 'com.google.guava:guava:15.0'
  compile 'commons-lang:commons-lang:2.6'
  compile 'org.apache.velocity:velocity:1.7'
  compile 'org.objenesis:objenesis:1.3'
  compile "org.codehaus.jackson:jackson-core-asl:$jackson_version"
  compile "org.codehaus.jackson:jackson-mapper-asl:$jackson_version"
  compile "org.springframework:spring-context:$spring_version"
  compile "org.springframework:spring-webmvc:$spring_version"
  compile 'javax.servlet:javax.servlet-api:3.0.1'

  compile files("${System.properties['java.home']}/../lib/tools.jar")
  //eww replicated from the original pom. The maven javadoc jars currently 404

  compile project(':annotations')

  testCompile 'junit:junit:4.11'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  testCompile 'org.mockito:mockito-core:1.9.5'
}

project(':annotations') {
  buildDir = "$rootProject.buildDir/$name"
}
